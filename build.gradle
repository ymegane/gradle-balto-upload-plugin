buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'junit:junit:4.12'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'org.apache.httpcomponents:httpmime:4.2.5'
    runtime 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    runtime 'org.apache.httpcomponents:httpmime:4.2.5'
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from "${buildDir}/javadoc"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.github.ymegane.balto'
            artifactId 'balto-upload'
            version '1.0.0'

            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url "$rootDir/docs/repository"
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = ['mavenJava']

    publish = true

    pkg {
        repo = 'maven'
        name = "gradle-balto-upload-plugin"
        desc = 'You can build and deploy your apps to Balto by running a single task.'
        websiteUrl = 'https://github.com/ymegane/gradle-balto-upload-plugin'
        issueTrackerUrl = 'https://github.com/ymegane/gradle-balto-upload-plugin/issues'
        vcsUrl = 'https://github.com/ymegane/gradle-balto-upload-plugin.git'
        licenses = ['Apache-2.0']
        githubRepo = "ymegane/gradle-balto-upload-plugin"
        githubReleaseNotesFile = 'README.md'

        version {
            name = '1.0.0'
            vcsTag = '1.0.0'

        }
    }
}

task getHomeDir << {
    println gradle.gradleHomeDir
}